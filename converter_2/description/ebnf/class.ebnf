/* Try it: https://bnfplayground.pauliankline.com/ */

/* -------------------------- Общие -------------------------- */

/* элементарные (или встроенные) конструкции */

<whs> ::= " "
<nln> ::= "\n"
/* допустимые значения для типа char, не только: */
<any_char> ::= ([A-Z] | [a-z] | [0-9] | "_")+

/* --- */

<optwhs> ::= <whs>*
<reqwhs> ::= <whs>+

/* множество допустимых для идентификатора символов PlantUML */
<ids_char> ::= ([A-Z] | [a-z] | [0-9] | "_")+
<string> ::= <any_char>+
<node_id> ::= <ids_char>+
<end_curly_brace> ::= "}"

/* -------------------------- Class -------------------------- */

/* стрелка */
<left_arrow_head> ::= "<" | "<|" | "*" | "o"
<rght_arrow_head> ::= ">" | "|>" | "*" | "o"
<arrow_direction> ::= ("l" | "r" | "d" | "u") | ("left" | "right" | "down" | "up")
<arrow_body> ::= ("." | "-")+ <arrow_direction> ("." | "-")+
<arrow> ::= <left_arrow_head>? <arrow_body> <rght_arrow_head>?

/* составные конструкции (не используются напрямую) */

<node> ::= <node_id>
<member_mark> ::= "+" | "#" | "-"

/* составные конструкции (то из чего состоит диаграмма, то что вводит пользователь) */

<enduml> ::= "@enduml"
<startuml> ::= "@startuml"
<directive> ::= <startuml> | <enduml>

/* --- */

<beg_interface> ::= "interface" <reqwhs> <node_id> "{"
<interface_member_func> ::= "+"? <ids_char>+ "(" (<ids_char>+ ",")+ ")"
<interface_body> ::= (<interface_member_func> <nln>)+ 
<interface> ::= <beg_interface> <nln> <interface_body> <end_curly_brace>

<beg_class> ::= "class" <reqwhs> <node_id> "{"
<class_member_data> ::= <member_mark> <ids_char>+ ":" <ids_char>+
<class_member_func> ::= <member_mark> <ids_char>+ "(" (<ids_char>+ ",")+ ")" <reqwhs> ":" <ids_char>+
<class_member> ::= <class_member_data> | <class_member_func>
<class_body> ::= (<class_member> <nln>)+
<class> ::= <beg_class> <nln> <class_body> <end_curly_brace>

<beg_enum> ::= "enum" <reqwhs> <node_id> "{"
<enum_value> ::= <ids_char>+
<enum_body> ::= (<enum_value> <nln>)+
<enum> ::= <beg_enum> <nln> (<enum_value> <nln>)+ <end_curly_brace>

<short_node> ::= ("class" | "enum" | "interface") <reqwhs> <node_id>
<whole_node> ::= <class> | <enum> | <interface>

/* --- */
<connection> ::= <node> <reqwhs> <arrow> <reqwhs> <node>

/* --- */

<diagram> ::= <startuml> <nln> ((<whole_node> | <short_node> | <connection>) <nln>)* <enduml> 