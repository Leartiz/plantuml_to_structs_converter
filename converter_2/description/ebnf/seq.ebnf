/* Try it: https://bnfplayground.pauliankline.com/ */

/* -------------------------- Общие -------------------------- */

/* элементарные (или встроенные) конструкции */

<whs> ::= " "
<nln> ::= "\n"
/* допустимые значения для типа char, не только: */
<any_char> ::= ([A-Z] | [a-z] | [0-9] | "_")+

/* --- */

<optwhs> ::= <whs>*
<reqwhs> ::= <whs>+

/* множество допустимых для идентификатора символов PlantUML */
<ids_char> ::= ([A-Z] | [a-z] | [0-9] | "_")+
<string> ::= <any_char>+
<node_id> ::= <ids_char>+
<end_curly_brace> ::= "}"

/* -------------------------- только для Rob -------------------------- */

/* стрелка */
<left_arrow_head> ::= "<" 
<rght_arrow_head> ::= ">"
<arrow_direction> ::= ("l" | "r" | "d" | "u") | ("left" | "right" | "down" | "up")
<arrow_body> ::= "-"+ <arrow_direction> "-"+
<arrow> ::= <left_arrow_head>? <arrow_body> <rght_arrow_head>?

/* составные конструкции (не используются напрямую) */

<part_whole_node> ::=   "\"" <string> "\"" <reqwhs> "as" <reqwhs> <node_id>
<node> ::= <node_id>

/* составные конструкции (то из чего состоит диаграмма, то что вводит пользователь) */

<enduml> ::= "@enduml"
<startuml> ::= "@startuml"
<directive> ::= <startuml> | <enduml>

/* --- */

/* между токенами можно убрать пробелы? */
<whole_node> ::= ("actor" | "boundary" | "entity" | "control") <reqwhs> <part_whole_node>
<short_node> ::= ("actor" | "boundary" | "entity" | "control") <reqwhs> <node_id>

/* --- */
<connection> ::= <node> <reqwhs> <arrow> <reqwhs> <node>

/* --- */

/* сторожевое условие */
<beg_fragment> ::= ("alt" | "opt" | "loop" ) <reqwhs> <any_char>+
<else_opd> ::= "else" <reqwhs> <any_char>+
<end_opd> ::= "end"
<fragment_body> ::= (<connection> <nln>)+ <else_opd>?
<fragment> ::= <beg_fragment> <nln> <fragment_body> <nln> <end_opd>

/* перечисление узлов */
<beg_ref_over> ::= "ref" <reqwhs> "over" <reqwhs> <any_char>+
<end_ref> ::= <end_opd> <reqwhs> "ref"
<ref_over> ::= <beg_ref_over> <nln> <string> <nln> <end_ref>

<grouping> ::= <fragment> | <ref_over>

/* --- */

<diagram> ::= <startuml> <nln> ((<whole_node> | <short_node> | <connection> | <fragment> | <ref_over>) <nln>)* <enduml> 